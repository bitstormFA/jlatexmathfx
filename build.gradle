import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.3.0'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'net.researchgate.release'


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

archivesBaseName = rootProject.name
group = 'com.proudapes'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'xml-apis', name: 'xml-apis-ext', version: '1.3.04'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}


signing {
    sign configurations.archives
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

task copyTemplates(type: Copy) {
    from 'src/templates'
    into rootDir
    filter(ReplaceTokens, tokens: [version: version])
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'jlatexmathfx'
                packaging 'jar'
                description 'A custom javafx control to render latex formula'
                url 'https://github.com/bitstormGER/jlatexmathfx'

                scm {
                    connection 'scm:git:https://github.com/bitstormGER/jlatexmathfx.git'
                    developerConnection 'scm:git:https://github.com/bitstormGER/jlatexmathfx.git'
                    url 'https://github.com/bitstormGER/jlatexmathfx.git'
                }

                licenses {
                    license {
                        name 'GNU General Public License, version 2'
                        url 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'bitstormger'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}


